plugins {
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.28.2"
    id "org.jetbrains.kotlin.jvm" version "1.7.0"
    id "org.jetbrains.kotlin.kapt" version "1.7.0"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

String environmental(String key)
{
    final String environment =
        System.getenv(key)

    if (environment != null)
    {
        return environment
    }

    return project.property(key)
}

allprojects {
    group "gg.scala.meetup"
    version "1.0.0"

    repositories {
        mavenCentral()

        maven {
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"

            content {
                includeGroup "org.bukkit"
                includeGroup "org.spigotmc"
            }
        }

        maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url = "https://oss.sonatype.org/content/repositories/central" }

        maven {
            name "Scala"
            url "${environmental("artifactory_contextUrl")}/gradle-release"

            credentials {
                username environmental("artifactory_user")
                password environmental("artifactory_password")
            }
        }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

        compileOnly "gg.scala.lemon:bukkit:1.2.1"
        compileOnly "gg.scala.cgs:common:1.1.3.1"
        compileOnly "gg.scala.spigot:sspigot-server:1.0.8"

        compileOnly "gg.scala.flavor:flavor:0.1.3.7"
        compileOnly "gg.scala.commons:bukkit:2.0.0"

        compileOnly "gg.scala.store:spigot:0.1.3"
        compileOnly "gg.scala.store:shared:0.1.3"

        compileOnly "gg.scala.cloudsync:spigot:1.0.1"

        compileOnly "me.lucko:helper:5.6.10.2"
        kapt "me.lucko:helper:5.6.10.2"
    }

    compileKotlin {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "1.8"
    }

    shadowJar {
        archiveClassifier.set("")

        exclude "**/*.kotlin_metadata"
        exclude "**/*.kotlin_builtins"
        exclude "META-INF/"

        archiveFileName = "uhc-meetup-${project.name}.jar"
    }

    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
            }
        }

        repositories {
            maven {
                name "Scala"
                url "${environmental("artifactory_contextUrl")}/gradle-release"

                credentials {
                    username environmental("artifactory_user")
                    password environmental("artifactory_password")
                }
            }
        }
    }

    tasks.build.dependsOn(
        shadowJar,
        publishShadowPublicationToScalaRepository
    )
}

artifactory {
    this.contextUrl = environmental("artifactory_contextUrl")

    publish {
        repository {
            repoKey = environmental("artifactory_release_local")
            username = environmental("artifactory_user")
            password = environmental("artifactory_password")
            maven = true
        }
    }
}
